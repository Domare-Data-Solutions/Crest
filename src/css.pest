
Css = { SOI ~ Rule* ~ EOI }

Rule = { Selector ~ "{" ~ PropertyList ~ "}" }

Selector = { CompoundSelector }
    CompoundSelector = { ComplexSelector ~ ("," ~ ComplexSelector)* }
    ComplexSelector = { SimpleSelector ~ Combination* }
        Combination = { Combinator ~ SimpleSelector }
        Combinator = @{ NextSibling | Child | Column | SubsequentSibling | Descendent | Namespace }
            NextSibling         = { "+" }
            Child               = { ">" }
            Column              = { "||" }
            SubsequentSibling   = { "~" }
            Namespace           = { "|" }
            Descendent          = @{ " " }
    // TODO: Implement { Attribute, Pseudoclass, Pseudoelement } Simple Selectors
    SimpleSelector = { (BasicSelector)* }
        BasicSelector = { TypeSelector | ClassSelector | IdSelector | UniversalSelector }
            IdSelector = { "#" ~ !("#") ~ BasicSelector }
            ClassSelector = { "." ~ !(".") ~ BasicSelector }
            TypeSelector = { Identifier }
            UniversalSelector = { "*" }

PropertyList = { Property* }
Property = { Identifier ~ ":" ~ ValueList ~ ";" }

ValueList = { Value ~ (","? ~ Value)* }
Value = { Function | Number | Identifier | String | Hex }
    Function = { Identifier ~ "(" ~ ValueList ~ ")" }
    Hex = @{ "#" ~ (ASCII_DIGIT | 'a'..'f' | 'A'..'F'){6} }
    String = { ("\"" ~ (!("\"") ~ ASCII)* ~ "\"") | ("'" ~ (!("'") ~ ASCII)* ~ "'") }
    Number = ${ Sign? ~ (Float | Int) ~ Unit? }
        Sign = { "+" | "-" }
        Int = { ASCII_DIGIT+ }
        Float = { ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
        Unit = { ("cm"|"mm"|"in"|"px"|"pt"|"pc") | ("em"|"ex"|"ch"|"rem"|"vw"|"vh"|"vmin"|"vmax"|"%") }

Identifier = { (ASCII_ALPHA | "_" | "-") ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

WHITESPACE = _{ (" " | "\t" | NEWLINE) }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
